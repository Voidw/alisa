grammar org.osate.assure.Assure with org.osate.alisa.common.Common//org.osate.results.Results//org.eclipse.xtext.common.Terminals

generate assure "http://www.osate.org/assure/Assure"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://www.osate.org/reqspec/ReqSpec" as ReqSpec
import "http://www.osate.org/alisa/common/Common" as Common
import "http://www.osate.org/alisa/workbench/Alisa" as Alisa
import "http://www.osate.org/verify/Verify" as Verify 
import  "http://www.osate.org/results/Results" as results
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

 
// result for a system in the architecture hierarchy
AssuranceCase:
	'case' name=QualifiedName 
	('plan' plan=[Alisa::AssurancePlan|QualifiedName])?
	'['
	metrics=Metrics
	('message' message= STRING)?
	modelResult += ModelResult+
	']'  
;

ModelResult:
	'model' name=QualifiedName 
	('for' target=[aadl2::ComponentImplementation|AadlClassifierReference])?
	'['
	metrics=Metrics
	('message' message= STRING)?
	claimResult += ClaimResult*
	subsystemResult += SubsystemResult*
	subAssuranceCase += [AssuranceCase|QualifiedName]*//[Alisa::AssurancePlan|QualifiedName]*
	']'  
;

SubsystemResult:
	'subsystem' name=QualifiedName 
	'for' targetSystem=ID
	'['
	metrics=Metrics
	('message' message= STRING)?
	(claimResult += ClaimResult)*
	(subsystemResult += SubsystemResult)*
	']'  
;


// Result of meeting a requirement
ClaimResult:
	'claim' target=[ReqSpec::Requirement|QualifiedName]
	'['
	metrics=Metrics
	('message' message= STRING)?
	(subClaimResult += ClaimResult)*
	(verificationActivityResult+= VerificationExpr)*
	']'  
;


PreconditionResult returns VerificationResult:
	'precondition' {PreconditionResult}	
	target=[Verify::VerificationMethod|QualifiedName]
	'['
	'executionstate'  executionState=VerificationExecutionState
	'resultstate' resultState=VerificationResultState
	('issues' '['
		issues+=ResultIssue*
		']'
	)?
	('report' resultReport=[results::ResultReport|QualifiedName])? // pointer to separate report
	metrics=Metrics
	('message' message= STRING)?
	']'  
;

ValidationResult returns VerificationResult:
	'validation' {ValidationResult}	
	target=[Verify::VerificationMethod|QualifiedName]
	'['
	'executionstate'  executionState=VerificationExecutionState
	'resultstate' resultState=VerificationResultState
	('issues' '['
		issues+=ResultIssue*
		']'
	)?
	('report' resultReport=[results::ResultReport|QualifiedName])? // pointer to separate report
	metrics=Metrics
	('message' message= STRING)?
	']'  
;


// result of executing a verification activity
VerificationActivityResult returns VerificationResult:
	'verification' {VerificationActivityResult}	target=[Verify::VerificationActivity|QualifiedName]
	'['
	'executionstate'  executionState=VerificationExecutionState
	'resultstate' resultState=VerificationResultState
	('issues' '['
		issues+=ResultIssue*
		']'
	)?
	('report' resultReport=[results::ResultReport|QualifiedName])? 
	metrics=Metrics
	('message' message= STRING)?
	preconditionResult= PreconditionResult?
	validationResult= ValidationResult?
	']'  
;

AssureResult:
	VerificationExpr | ModelResult | SubsystemResult |
		AssuranceCase | ClaimResult  | PreconditionResult | ValidationResult
	;

enum ElseType: 
	OK='ok' | FAIL='fail' | TIMEOUT='timeout' | ERROR='error'
;
	
VerificationExpr : VerificationActivityResult | ElseResult | ThenResult;

ElseResult returns ElseResult:
	'else'  first+=VerificationExpr+ ('error' error+=VerificationExpr+)?
	('fail' fail+=VerificationExpr+)? ('timeout' timeout+=VerificationExpr+)?
	'['
	didFail=ElseType?
	metrics=Metrics
	']'
;

ThenResult returns ThenResult:
	'then' first+=VerificationExpr+ 'do' second+=VerificationExpr+
	'['
	( didThenFail?='thenfailed')?
	metrics=Metrics
	']'
;

Metrics returns Metrics:
	{Metrics}('tbdcount' tbdCount=INT)?
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('timeoutcount'  timeoutCount=INT)?
	('errorcount' errorCount=INT)? 
	('didelsecount' didelseCount=INT)?  // else branch executed
	('thenskipcount' thenskipCount=INT)?
	('prefailcount'  preconditionfailCount=INT)?
	('validfailcount'  validationfailCount=INT)?
	('weight' weight = INT)?
;


enum VerificationResultState :
	TBD='tbd'| SUCCESS='success' | FAIL='fail' | ERROR='error' | TIMEOUT='timeout' ;



enum  VerificationExecutionState:
	TODO='todo'  // verification to be performed
	| RUNNING='running' // verification in progress (background processing)
	| REDO='redo' // invalidated while in progress. redo verification.
	| COMPLETED='completed' // completed. verification result is valid
;


