grammar org.osate.assure.Assure with org.osate.alisa.common.Common

generate assure "http://www.osate.org/assure/Assure"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://aadl.info/AADL/2.0/instance" as instance
import "http://www.osate.org/reqspec/ReqSpec" as ReqSpec
import "http://www.osate.org/verify/Verify" as Verify
import  "http://www.osate.org/categories/Categories" as categories
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
// We assume that conditional selection has occurred during instantiation
 
// result for a system in the architecture hierarchy
CaseResult:
	'case' name=ID 
	'for' target=[aadl2::Classifier|AadlClassifierReference]
	'instance' instance= [instance::InstanceObject|URIID]
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('message' sucessMsg= STRING)?
	(subCaseResult += CaseResult)*
	(claimResult += ClaimResult)*
	(hazardResult += HazardResult)*
	']'  
;

// Result of meeting a requirement
ClaimResult:
	'claim' name=ID 'for' target=[ReqSpec::Requirement|QualifiedName]
	('instance' instance= [instance::InstanceObject|URIID])? // if the requirement is for an element of the component instance
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('successMsg' sucessMsg= STRING)?
	(subClaimResult += ClaimResult)*
	(verificationActivityResult+= VerificationExpr)*
	']'  
;

// result of all requirements of a hazard being met
HazardResult:
	'hazard' name=ID 'for' target=[ReqSpec::Hazard|QualifiedName]
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('message' sucessMsg= STRING)?
	(claimResult+= ClaimResult)*
	']'  
;

AssumptionResult:
	'assumption' 	name=ID 
	'for' target= [Verify::VerificationAssumption|QualifiedName]
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('message' sucessMsg= STRING)?
	(verificationActivityResult+= VerificationExpr)*
	']'  
;

PreconditionResult:
	'precondition' 	name=ID 
	'for' target=[Verify::VerificationPrecondition|QualifiedName]
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)?
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('message' sucessMsg= STRING)?
	(verificationActivityResult+= VerificationExpr)*
	']'  
;


// result of executing a verification activity
VerificationActivityResult:
	'verification' 	name=ID 
	'for' target=[Verify::VerificationActivity|QualifiedName]
	'['
	('executionstate'  executionState=VerificationExecutionState)
	(result=VerificationResult)
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	('message' sucessMsg= STRING)?
	(assumptionResult+= AssumptionResult)*
	(preconditionResult+= PreconditionResult)*
	']'  
;

AssureResult:
	VerificationExpr |
		CaseResult | ClaimResult | HazardResult | PreconditionResult | AssumptionResult
	;

VerificationExpr returns VerificationExpr:
	VerificationActivityResult |
	('failthen' {FailThenResult}|'andthen' {AndThenResult}) first+=VerificationExpr 'do' second+=VerificationExpr
	'['
	('success'  successCount=INT)?
	('fail'  failCount=INT)?
	('error' errorCount=INT)? 
	('failthen' failthenCount=INT)?
	('skipped' skippedCount=INT)?
	('total' totalCount=INT)?
	('weight' weight = INT)?
	']'
;

VerificationResult returns VerificationResult:
	'resultstate' resultState=VerificationResultState
	('failtype' type=STRING)?
	('failmessage' failMsg=STRING)?
	('failtarget' failTarget=[ecore::EObject|QualifiedName])?
	('report' resultReport=[ecore::EObject|QualifiedName])? // pointer to separate report
;



enum VerificationResultState :
	TBD='tbd'| SUCCESS='success' | FAIL='fail' | error='error' ;



enum  VerificationExecutionState:
	TODO='todo'  // verification to be performed
	| RUNNING='running' // verification in progress (background processing)
	| REDO='redo' // invalidated while in progress. redo verification.
	| COMPLETED='completed' // completed. verification result is valid
;


