grammar org.osate.assure.Assure with org.eclipse.xtext.common.Terminals

generate assure "http://www.osate.org/assure/Assure"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://aadl.info/AADL/2.0/instance" as instance
import "http://www.osate.org/reqspec/ReqSpec" as ReqSpec
import "http://www.osate.org/verify/Verify" as Verify 
import  "http://www.osate.org/categories/Categories" as categories
import  "http://www.osate.org/results/Results" as results
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

 
// result for a system in the architecture hierarchy
CaseResult:
	'case' name=ID 
	'for' target=[aadl2::Classifier|AadlClassifierReference]
	'instance' instance= [instance::InstanceObject|URIID]
	'['
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	('message' message= STRING)?
	(subCaseResult += CaseResult)*
	(claimResult += ClaimResult)*
	']'  
;

// Result of meeting a requirement
ClaimResult:
	'claim' name=ID 'for' target=[ReqSpec::Requirement|QualifiedName]
	('instance' instance= [instance::InstanceObject|URIID])? // if the requirement is for an element of the component instance
	'['
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	('message' message= STRING)?
	(subClaimResult += ClaimResult)*
	(verificationActivityResult+= VerificationExpr)*
	']'  
;


AssumptionResult:
	'assumption' 	name=ID 
	'for' target= [Verify::VerificationAssumption|QualifiedName]
	'['
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	('message' message= STRING)?
	(verificationActivityResult+= VerificationExpr)*
	']'  
;

PreconditionResult:
	'precondition' 	name=ID 
	'for' target=[Verify::VerificationPrecondition|QualifiedName]
	'['
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)?
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	('message' message= STRING)?
	(verificationActivityResult+= VerificationExpr)*
	']'  
;


// result of executing a verification activity
VerificationActivityResult:
	'verification' 	name=ID 
	'for' target=[Verify::VerificationActivity|QualifiedName]
	'['
	('executionstate'  executionState=VerificationExecutionState)
	'resultstate' resultState=VerificationResultState
	('issues' '['
		issues+=ResultIssue*
		']'
	)?
	('report' resultReport=[results::ResultReport|QualifiedName])? // pointer to separate report
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	('message' message= STRING)?
	(assumptionResult+= AssumptionResult)*
	(preconditionResult+= PreconditionResult)*
	']'  
;

AssureResult:
	VerificationExpr |
		CaseResult | ClaimResult  | PreconditionResult | AssumptionResult
	;
	
	VerificationExpr : VerificationActivityResult | FailThenResult | AndThenResult;

FailThenResult returns FailThenResult:
	'failthen'  first+=VerificationExpr 'do' second+=VerificationExpr
	'['
	( didFailThenFail?='failthenfailed')?
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	']'
;
AndThenResult returns AndThenResult:
	'andthen' first+=VerificationExpr 'do' second+=VerificationExpr
	'['
	( didAndThenFail?='andthenfailed')?
	('successcount'  successCount=INT)?
	('failcount'  failCount=INT)?
	('errorcount' errorCount=INT)? 
	('failthencount' failthenCount=INT)?
	('andthencount' andthenCount=INT)?
	('tbdcount' tbdCount=INT)?
	('weight' weight = INT)?
	']'
;

// use it for failures, warnings and successes.
ResultIssue:
	issueType=ResultIssueType (':' name=ID)?
	message=STRING
	('exception' exceptionType = STRING)?
	('target' target=[ecore::EObject|URIID])?
	('[' issues +=ResultIssue+']')?
;

enum ResultIssueType:
	ERROR='error' | WARNING='warning'| SUCCESS='success' | INFO='info'
;


enum VerificationResultState :
	TBD='tbd'| SUCCESS='success' | FAIL='fail' | ERROR='error' ;



enum  VerificationExecutionState:
	TODO='todo'  // verification to be performed
	| RUNNING='running' // verification in progress (background processing)
	| REDO='redo' // invalidated while in progress. redo verification.
	| COMPLETED='completed' // completed. verification result is valid
;

// Qualified classifier reference
AadlClassifierReference:
	ID '::' ID ('.' ID)?;


URIID: STRING;	

QualifiedName : ID ( => '.' ID )*
	
;
