grammar org.osate.assure.Assure with org.osate.alisa.common.Common//org.eclipse.xtext.common.Terminals

generate assure "http://www.osate.org/assure/Assure"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://aadl.info/AADL/2.0/instance" as instance
import "http://www.osate.org/reqspec/ReqSpec" as ReqSpec
import "http://www.osate.org/verify/Verify" as Verify

// We assume that conditional selection has occurred during instantiation
 
// result for a system in the architecture hierarchy
CaseResult:
	'case' name=ID 
	'for' target=[instance::InstanceObject|URIID]
	'['
	('pass'  passCount=INT)
	('fail'  failCount=INT)
	('neutral' neutralCount=INT)
	('unknown' unknownCount=INT) 
	('weight' weight = INT)?
	('successMsg' sucessMsg= MultiLineString)?
	('failMsg' failMsg = MultiLineString)?
	(subCaseResult += CaseResult)*
	(claimResult += EvidenceExpr)*
	(hazardResult += HazardResult)*
	']'  
;

// result of executing a verification activity
VerificationResult:
	'verification' 	name=ID 
	'of' verificationActivity=[Verify::VerificationActivity|QualifiedName]
	'for' target=[instance::InstanceObject|URIID]
	'['
	('output'  title=STRING)?
	('logfile'  description=STRING)?
	('date'  date=STRING)?
	('state'  state=VerificationResultState)?
	('status'  status=VerificationResultStatus)? 
	('weight' weight = INT)?
	('successMsg' sucessMsg= MultiLineString)?
	('failMsg' failMsg = MultiLineString)?
	(assumptionResult+= AssumptionResult)*
	(preconditionResult+= PreconditionResult)*
	']'  
;
AssumptionResult:
	'assumption' 	name=ID 
	'of' verificationActivity= [Verify::VerificationAssumption|QualifiedName]
	'for' target=[instance::InstanceObject|URIID]
	'['
	('output'  title=STRING)?
	('logfile'  description=STRING)?
	('date'  date=STRING)?
	('state'  state=VerificationResultState)?
	('status'  status=VerificationResultStatus)? 
	('weight' weight = INT)?
	('successMsg' sucessMsg= MultiLineString)?
	('failMsg' failMsg = MultiLineString)?
	']'  
;

PreconditionResult:
	'precondition' 	name=ID 
	'of' verificationActivity=[Verify::VerificationPrecondition|QualifiedName]
	'for' target=[instance::InstanceObject|URIID]
	'['
	('output'  title=STRING)?
	('logfile'  description=STRING)?
	('date'  date=STRING)?
	('state'  state=VerificationResultState)?
	('status'  status=VerificationResultStatus)? 
	('weight' weight = INT)?
	('successMsg' sucessMsg= MultiLineString)?
	('failMsg' failMsg = MultiLineString)?
	']'  
;

AssuranceResult: EvidenceResult | ClaimResult | HazardResult | CaseResult;
EvidenceResult : VerificationResult | AssumptionResult | PreconditionResult;

// Result of meeting a requirement
ClaimResult:
	'claim' name=ID 'of' requirement=[ReqSpec::Requirement|QualifiedName]
	'for' target=[instance::InstanceObject|URIID]
	'['
	('success'  successCount=INT)
	('fail'  failCount=INT)
	('neutral' neutralCount=INT)
	('unknown' unknownCount=INT) 
	'successMsg' sucessMsg= MultiLineString
	'failMsg' failMsg = MultiLineString
	(subClaimResult += ClaimResult)*
	(verificationResult+= VerificationResult)*
	']'  
;

// result of all requirements of a hazard being met
HazardResult:
	'hazard' name=ID 'of' hazard=[ReqSpec::Hazard|QualifiedName]
	'for' target=[instance::InstanceObject|URIID]
	'['
	('pass'  passCount=INT)
	('fail'  failCount=INT)
	('neutral' neutralCount=INT)
	('unknown' unknownCount=INT) 
	(verificationResult+= VerificationResult)*
	']'  
;

EvidenceExpr:
	FailThen | ClaimResult
;

FailThen:
	'fail' first+=ClaimResult 'then' second+=ClaimResult
;

IfThen:
	'if' first+=ClaimResult 'then' second+=ClaimResult
;



enum  VerificationResultState:
	TODO='todo'  // verification to be performed
	| RUNNING='running' // verification in progress (background processing)
	| REDO='redo' // invalidated while in progress. redo verification.
	| COMPLETED='completed' // completed. verification result is valid
;

enum VerificationResultStatus:
	TBD='tbd' | SUCCESS='success' | FAIL='fail' | UNKNOWN='unknown' 
;
