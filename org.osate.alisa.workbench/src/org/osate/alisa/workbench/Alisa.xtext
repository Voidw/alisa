//grammar org.osate.alisa.workbench.Alisa with org.osate.alisa.common.Common
grammar org.osate.alisa.workbench.Alisa with org.eclipse.xtext.common.Terminals

generate alisa "http://www.osate.org/alisa/workbench/Alisa"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://www.osate.org/verify/Verify" as Verify 
import "http://www.osate.org/categories/Categories" as categories

AlisaWorkArea:
	'alisa' name=ID
	cases+=AssuranceEvidenceConfiguration*;

AssuranceEvidenceConfiguration:
	'assurance' 'task' name=ID (':' title=STRING)?
	'for' system=[aadl2::ComponentImplementation|AadlClassifierReference]
	'['
	(
	('description' description=Description)?
//	& (  constants+=XValDeclaration* )
	& ('assure' plans+=[Verify::VerificationPlan|QualifiedName]+)
	& ('assume' 'verified' verifiedAssumption+=[aadl2::ComponentClassifier|AadlClassifierReference]+)?
	&('filter' (selectionFilter+=[categories::SelectionCategory|ID])+)?
	)
	']';
	
Description : 
	( description += DescriptionElement  )+
;

DescriptionElement :
 	text = STRING | thisTarget?='this'
 ;


 
// Qualified classifier reference
AadlClassifierReference:
	ID '::' ID ('.' ID)?;

QualifiedName:
	ID ('.' ID)?;
	
URIID: STRING;		