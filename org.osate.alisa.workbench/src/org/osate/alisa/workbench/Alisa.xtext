grammar org.osate.alisa.workbench.Alisa with org.osate.alisa.common.Common

generate alisa "http://www.osate.org/alisa/workbench/Alisa"
import "http://aadl.info/AADL/2.0" as aadl2
import "http://www.osate.org/verify/Verify" as Verify 
import "http://www.osate.org/categories/Categories" as categories

AlisaWorkArea:
	cases+=(AssurancePlan|AssuranceTask)*;
 
AssurancePlan:
	'assurance' 'plan' name=QualifiedName (':' title=STRING)?
	'for' target=[aadl2::ComponentImplementation|AadlClassifierReference]
	'['
	(
	(description=Description)?
	& ('assure' assure+=[Verify::VerificationPlan|QualifiedName]+)?
	& ('assure' 'subsystem' assureSubsystems+=[aadl2::Subcomponent|ID]+|assureAll?='all')?
	& ('assume' 'subsystem' (assumeSubsystems+=[aadl2::Subcomponent|ID]+|assumeAll?='all'))?
		& ('issues' issues+=STRING+ )?
	)
	']';
	
	AssuranceTask returns categories::CategoryFilter :
		{AssuranceTask} 'assurance''task' name=ID (':' title=STRING)? 'for' assurancePlan=[AssurancePlan|QualifiedName]
		'['
	(
		('requirement''type' requirementType+=[categories::RequirementType|ID]+ anyRequirementType?='any'?)?
		&('method''type' methodType+=[categories::MethodType|ID]+ anyMethodType?='any'?)?
		&('user''selection' userSelection+=[categories::UserSelection|ID]+ anyUserSelection?='any'?)?
		&('quality''attribute' qualityAttribute+=[categories::QualityAttribute|ID]+ anyQualityAttribute?='any'?)?
		&('development''phase' developmentPhase+=[categories::DevelopmentPhase|ID]+ anyDevelopmentPhase?='any'?)?
		&('issues' issues+=STRING+ )?
	)
	']'
	;
