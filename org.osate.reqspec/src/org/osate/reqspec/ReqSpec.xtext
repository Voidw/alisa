grammar org.osate.reqspec.ReqSpec with org.osate.alisa.common.Common

generate reqSpec "http://www.osate.org/reqspec/ReqSpec"

import "http://aadl.info/AADL/2.0" as aadl2
import  "http://www.osate.org/categories/Categories" as categories
import  "http://www.osate.org/organization/Organization" as org
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

ReqSpec: parts +=(ReqSpecs|StakeholderGoals|ReqDocument)+  ;
//ReqSpec: ReqSpecs|StakeholderGoals|ReqDocument  ;

ReqSpecContainer: ReqSpecs | GoalFolder | StakeholderGoals | ReqDocument | DocumentSection;

ContractualElement: Goal | Requirement ;

ReqRoot:  StakeholderGoals | ReqDocument | ReqSpecs;


StakeholderGoals: 'stakeholder' 'goals' name=ID (':' title=STRING)?
	( 'for' (target=[aadl2::Classifier|AADLCLASSIFIERREFERENCE] | targetDescription=STRING| global?='all'))?
	 (  description=Description)? 
	'[' content+=( Goal | GoalFolder)* 
		('issues' issues+=STRING+ )?
	']'
;

ReqDocument: 'document' name=ID (':' title=STRING)?
	 (  description=Description)? 
	'[' (content+= (Goal | Requirement |  DocumentSection))*
		('issues' issues+=STRING+ )?
	 ']'
;
DocumentSection returns DocumentSection:
	'section' label =ID (':' title=STRING)?
	 (  description=Description)? 
	'[' (content+= (Goal | Requirement |  DocumentSection))*
	']' 
	;

ReqSpecs returns ReqSpecs:
	'requirement' 'specification' name =ID (':' title=STRING)?
//	('import' importedNamespace=QualifiedNameWithWildCard)?
	( 'for' (target=[aadl2::Classifier|AADLCLASSIFIERREFERENCE] | targetDescription=STRING| global?='all'))?
	('include' otherreqspecs += [ReqSpecs|QualifiedName]+)?
	'[' 
	(
		( constants+=XValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	& (content+= (Requirement))*
	)
		('issues' issues+=STRING+ )?
	']'
	;



// does not add to a qualified name
GoalFolder returns GoalFolder:
	'folder' label =ID (':' title=STRING)?
	'[' (content+= (Goal |  GoalFolder))*
	']'
	;

	

Goal returns Goal:
	'goal' name=ID (':'  title=STRING )? // short string for title
	( 'for' (targetElement=[aadl2::NamedElement|ID] | targetDescription=STRING|
		(target=[aadl2::Classifier|AADLCLASSIFIERREFERENCE] ('item' targetElement=[aadl2::NamedElement|ID])?  )
		)
	)?
'['  (
	 ('category'  category+=[categories::RequirementCategory|ID]+)?
	 &(  description=Description)? 
	 &(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	 &('refines' refinesReference+=[Goal|QualifiedName]+)? 
	 &('conflicts' 'with' conflictsReference+=[Goal|QualifiedName]+)? 
	 &('stakeholder'  stakeholderReference+=[org::Stakeholder|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	 ']';


// requirement without target acts as a general requirement/constraint
// becomes a "global" constraint that still takes model elements that are supplied by query 	
Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for' (targetElement=[aadl2::NamedElement|ID] | targetDescription=STRING
		|(target=[aadl2::Classifier|AADLCLASSIFIERREFERENCE] ('item' targetElement=[aadl2::NamedElement|ID])?  )
		)
	)?
'['	(
 	('category'  category+=[categories::RequirementCategory|ID]+ )?
	&(  description=Description)? 
	& (  constants+=XValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
	&('see' 'goal'  goalReference+=[Goal|QualifiedName]+)?
	&('see' 'document' 'goal'  stakeholderRequirementReference+=[Goal|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';

ReqPredicate: InformalPredicate | XPredicate | ValueAssertion | InputAssumption | OutputGuarantee| BehaviorEquation;

InformalPredicate: 'assert' description=STRING ;
XPredicate: 'assert' 'predicate' xpression=XExpression ;

ValueAssertion: 'assert' 'value' xpression=XExpression;	
InputAssumption:  'input' 'assumption' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
OutputGuarantee: 'output''guarantee' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
BehaviorEquation: 'assert' 'invariant' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);

ExternalDocument : 
	docReference=DOCPATH ( '#' docFragment=DOCFRAGMENT)?
;


// path to external document and its fragment
DOCPATH: ( ID '/')* ID '.' ID;

DOCFRAGMENT: ID ('.' ID)*;

	