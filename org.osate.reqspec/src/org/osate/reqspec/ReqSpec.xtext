grammar org.osate.reqspec.ReqSpec with org.osate.alisa.common.Common

generate reqSpec "http://www.osate.org/reqspec/ReqSpec"

import "http://aadl.info/AADL/2.0" as aadl2
import  "http://www.osate.org/categories/Categories" as categories
import  "http://www.osate.org/organization/Organization" as org
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ReqSpec: parts +=(SystemRequirements|RequirementLibrary|StakeholderGoals|ReqDocument|GlobalConstants)+  ;

ReqSpecContainer: SystemRequirements | RequirementLibrary |  StakeholderGoals | ReqDocument | DocumentSection ;

GlobalConstants: 
	'constants' name=QualifiedName '['
	constants+=ValDeclaration* ']'
;

ContractualElement: Goal | DocGoal| Requirement |DocRequirement ;

ReqRoot:  StakeholderGoals | ReqDocument | SystemRequirements | RequirementLibrary;


StakeholderGoals: 'stakeholder' 'goals' name=QualifiedName (':' title=STRING)?
	( 'for' (target=[aadl2::ComponentClassifier|AadlClassifierReference] | componentCategory+=ComponentCategory+ | global?='all'))
	('use' 'constants' importConstants+=[GlobalConstants|QualifiedName]+)?
	'[' 
	(	description=Description? 
		& constants+=ValDeclaration*
		&content+=Goal* 
	    &('documents'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
				)
	']'
;

ReqDocument: 'document' name=QualifiedName (':' title=STRING)?
	'[' 
	( (  description=Description)? 
	&(content+= (DocGoal | DocRequirement |  DocumentSection))*
	 &('documents'  docReference+=ExternalDocument+)? 
	&('issues' issues+=STRING+ )?
	)
	 ']'
;
DocumentSection returns DocumentSection:
	'section' label =ID (':' title=STRING)?
	'[' (
	 (  description=Description)? 
	
	&(content+= (DocGoal | DocRequirement |  DocumentSection))*
	)
	']' 
	;

SystemRequirements returns SystemRequirements:
	'system' 'requirements'  name =QualifiedName (':' title=STRING)? 
	'for' target=[aadl2::ComponentClassifier|AadlClassifierReference] 
	('use' 'constants' importConstants+=[GlobalConstants|QualifiedName]+)?
	'[' 
	(
	 description=Description? 
	&constants+=ValDeclaration*
	& computes+=ComputeDeclaration* 
	& content+= (SystemRequirement)*
	 &('document'  docReference+=ExternalDocument+)? 
	 &('stakeholder' 'goals'  stakeholderGoals+=[ReqRoot|QualifiedName]+)? 
	&('issues' issues+=STRING+ )?
	)
	']'
	;

RequirementLibrary returns RequirementLibrary:
	'requirements'  name =QualifiedName (':' title=STRING)? 
	('use' 'constants' importConstants+=[GlobalConstants|QualifiedName]+)?
	'[' 
	(
	 description=Description? 
	&constants+=ValDeclaration*
	& computes+=ComputeDeclaration* 
	& content+= (Requirement)*
	 &('document'  docReference+=ExternalDocument+)? 
	 &('stakeholder' 'goals'  stakeholderGoals+=[ReqRoot|QualifiedName]+)? 
	&('issues' issues+=STRING+ )?
	)
	']'
	;


DocGoal returns Goal:
	'goal' name=ID (':'  title=STRING )? // short string for title
	( 'for' ( targetDescription=STRING | (target=[aadl2::ComponentClassifier|AadlClassifierReference] ( targetElement=[aadl2::NamedElement|ID])?  )
		)
	)?
'['  (
 	('category'  category+=[categories::RequirementCategory|ID]+ )?
 	&('quality'  category+=[categories::QualityCategory|ID]+ )?
 	&('selection'  category+=[categories::SelectionCategory|ID]+ )?
	 & description=Description? 
	&constants+=ValDeclaration* 
	 &rationale=Rationale? 
	 &changeUncertainty=Uncertainty? 
	 &('refines' refinesReference+=[Goal|QualifiedName]+)? 
	 &('conflicts' 'with' conflictsReference+=[Goal|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('stakeholder'  stakeholderReference+=[org::Stakeholder|QualifiedName]+)?
	 &( 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	 ']';


Goal returns Goal:
	'goal' name=ID (':'  title=STRING )? // short string for title
	( 'for'  targetElement=[aadl2::NamedElement|ID]
	)?
'['  (
 	('category'  category+=[categories::RequirementCategory|ID]+ )?
 	&('quality'  category+=[categories::QualityCategory|ID]+ )?
 	&('selection'  category+=[categories::SelectionCategory|ID]+ )?
	 &(  description=Description)? 
	& (  constants+=ValDeclaration* )
	 &(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	 &('refines' refinesReference+=[Goal|QualifiedName]+)? 
	 &('conflicts' 'with' conflictsReference+=[Goal|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('stakeholder'  stakeholderReference+=[org::Stakeholder|QualifiedName]+)?
	 &('document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	 ']';

SystemRequirement returns Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for'  (
		targetElement=[aadl2::NamedElement|ID] )
	)?
'['	(
 	('category'  category+=[categories::RequirementCategory|ID]+ )?
 	&('quality'  category+=[categories::QualityCategory|ID]+ )?
 	&('selection'  category+=[categories::SelectionCategory|ID]+ )?
	&(  description=Description)? 
	& (  constants+=ValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
	&('decomposes' decomposesReference+=[Requirement|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('development' 'stakeholder'  developmentStakeholder+=[org::Stakeholder|QualifiedName]+)?
	&('goal'  goalReference+=[Goal|QualifiedName]+)?
	 &('document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';
	
Requirement returns Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for'  (
		componentCategory+=ComponentCategory+| global?='all')
	)?
'['	(
 	('category'  category+=[categories::Category|ID]+ )?
	&(  description=Description)? 
	& (  constants+=ValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
	&('decomposes' decomposesReference+=[Requirement|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('development' 'stakeholder'  developmentStakeholder+=[org::Stakeholder|QualifiedName]+)?
	&('goal'  goalReference+=[Goal|QualifiedName]+)?
	 &('document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';
	
DocRequirement returns Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for'  ( targetDescription=STRING |
		(target=[aadl2::ComponentClassifier|AadlClassifierReference] ( targetElement=[aadl2::NamedElement|ID])?  ) |
		componentCategory+=ComponentCategory+| global?='all')
	)?
'['	(
 	('category'  category+=[categories::Category|ID]+ )?
	&(  description=Description)? 
	& (  constants+=ValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
	&('decomposes' decomposesReference+=[Requirement|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('development' 'stakeholder'  developmentStakeholder+=[org::Stakeholder|QualifiedName]+)?
	&('goal'  goalReference+=[Goal|QualifiedName]+)?
	 &('document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';

ReqPredicate: InformalPredicate | Predicate | ValuePredicate;// | InputAssumption | OutputGuarantee| BehaviorEquation;

InformalPredicate: 'informal' 'predicate'  description=STRING ;
Predicate: 'predicate' xpression=AExpression ;

ValuePredicate: 'value' 'predicate' xpression=AAndExpression;	// intended to be restricted to deal with value limits



//InputAssumption:  'input' 'assumption' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
//OutputGuarantee: 'output''guarantee' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
//BehaviorEquation: 'invariant' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);

ExternalDocument : 
	docReference=DOCPATH ( '#' docFragment=DOCFRAGMENT)?
;


// path to external document and its fragment
DOCPATH: ( ID '/')* ID '.' ID;

DOCFRAGMENT: ID ('.' ID)*;

	