grammar org.osate.categories.Categories with org.eclipse.xtext.common.Terminals

generate categories "http://www.osate.org/categories/Categories"

Categories : RequirementCategories | HazardCategories | VerificationCategories | SelectionCategories
;
/**
 * requirement categories uses a label instead of name to make the category itself global
 */
RequirementCategories :
	'requirement''categories' 
	'[' category += RequirementCategory+
	']'
;

HazardCategories :
	'hazard' 'categories' 
	'[' category += HazardCategory+
	']'
;

VerificationCategories :
	'verification' 'categories' 
	'[' category += VerificationCategory+
	']'
;

SelectionCategories :
	'selection' 'categories' 
	'[' category += SelectionCategory+
	']'
;


Category : RequirementCategory | VerificationCategory | HazardCategory | SelectionCategory;

/**
 * Requirement category indicates the kind of requirement we are dealing with
 */
RequirementCategory :
	name = ID ('extends' extends=[RequirementCategory|CatRef])? 
;


/**
 * Hazard category indicates the kind of hazard we are dealing with
 */
HazardCategory :
	name = ID ('extends' extends=[HazardCategory|CatRef])? 
;


/**
 * Verification category indicates the kind of verification we are dealing with
 */
VerificationCategory :
	name = ID ('extends' extends=[VerificationCategory|CatRef])? 
;

/**
 * selection category indicates a tag for filtered selection of verification activities
 */
SelectionCategory :
	name = ID ('extends' extends=[SelectionCategory|CatRef])? 
;


// Category reference. Currently it is only a single ID
CatRef:
	ID //('.' ID)?
	;	

