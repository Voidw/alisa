grammar org.osate.categories.Categories with org.eclipse.xtext.common.Terminals

generate categories "http://www.osate.org/categories/Categories"

Categories : RequirementCategories  | VerificationCategories | SelectionCategories
;
/**
 * requirement categories uses a label instead of name to make the category itself global
 */
RequirementCategories :
	'requirement''categories' 
	'[' category += RequirementCategory+
	']'
;

VerificationCategories :
	'verification' 'categories' 
	'[' category += VerificationCategory+
	']'
;

SelectionCategories :
	'selection' 'categories' 
	'[' category += SelectionCategory+
	']'
;


Category : RequirementCategory | VerificationCategory  | SelectionCategory;

/**
 * Requirement category indicates the kind of requirement we are dealing with
 */
RequirementCategory :
	name = ID ('{' subCategories+=[RequirementCategory|CatRef]* '}')? 
;




/**
 * Verification category indicates the kind of verification we are dealing with
 */
VerificationCategory :
	name = ID ('{' subCategories+=[VerificationCategory|CatRef]*'}')? 
;

/**
 * selection category indicates a tag for filtered selection of verification activities
 */
SelectionCategory :
	name = ID ('{' subCategories+=[SelectionCategory|CatRef]*'}')? 
;


// Category reference. Currently it is only a single ID
CatRef:
	ID //('.' ID)?
	;	

