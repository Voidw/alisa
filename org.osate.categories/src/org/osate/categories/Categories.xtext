grammar org.osate.categories.Categories with org.eclipse.xtext.common.Terminals

generate categories "http://www.osate.org/categories/Categories"

CategoriesDefinitions : requirementTypeCategories=RequirementTypeCategories?   & methodTypeCategories=MethodTypeCategories? 
	& selectioncategories=UserSelectionCategories? & developmentPhaseCategories=DevelopmentPhaseCategories? 
	& qualityAttributeCategories=QualityAttributeCategories? //&  categoryFilters=CategoryFilters?
;

RequirementTypeCategories returns Categories:
	{RequirementTypeCategories}'requirement''type' 
	'[' category += RequirementType+
	']'
;

MethodTypeCategories returns Categories:
	{MethodTypeCategories}'method' 'type' 
	'[' category += MethodType+
	']'
;

UserSelectionCategories returns Categories:
	{UserSelectionCategories}'user''selection'  
	'[' category += UserSelection+
	']'
;

QualityAttributeCategories returns Categories:
	{QualityAttributeCategories}'quality' 'attribute'
	'[' category += QualityAttribute+
	']'
;

DevelopmentPhaseCategories returns Categories:
	{DevelopmentPhaseCategories}'development''phase'  
	'[' category += DevelopmentPhase+
	']'
;


/**
 * Requirement category indicates the type of requirement
 */
RequirementType returns Category:
	{RequirementType} name = ID  
;




/**
 * Method type indicates the type of verification method
 */
MethodType returns Category:
	{MethodType} name = ID  
;

/**
 * user selection  indicates a user defined selection categories  
 */
UserSelection returns Category:
	{UserSelection} name = ID  
;

/**
 * quality attribute indicates a quality attribute 
 */
QualityAttribute returns Category:
	{QualityAttribute} name = ID  
;

/**
 * phase category indicates a development phase 
 */
DevelopmentPhase returns Category:
	{DevelopmentPhase} name = ID  
;

//
//CategoryFilters returns CategoryFilters:
//	'filters' 
//	filters+=NamedCategoryFilter+ 
//;
//
CategoryFilter returns CategoryFilter:
	name=ID '['
	('requirement''type' requirementType+=[RequirementType|ID]+ anyRequirementType?='any'?)?
	&('method''type' methodType+=[MethodType|ID]+ anyMethodType?='any'?)?
	&('user''selection' userSelection+=[UserSelection|ID]+ anyUserSelection?='any'?)?
	&('quality''attribute' qualityAttribute+=[QualityAttribute|ID]+ anyQualityAttribute?='any'?)?
	&('development''phase' developmentPhase+=[DevelopmentPhase|ID]+ anyDevelopmentPhase?='any'?)?
	']'
;



// Category reference. Currently it is only a single ID
CatRef:
	ID //('.' ID)?
	;	

